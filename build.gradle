ext.configFile = file 'build.properties'

configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
    def mcpVersion = project.config.mappings_version.replace('snapshot_', '')
    config.srgDir = file("${project.gradle.gradleUserHomeDir}/caches/minecraft/de/oceanlabs/mcp/mcp_snapshot/${mcpVersion}/${config.minecraft_version}/srgs/")
    config.mcp_srg = new File(config.srgDir, 'mcp-srg.srg')
    config.srg_mcp = new File(config.srgDir, 'srg-mcp.srg')
}

subprojects {
    project.ext.config = rootProject.ext.config
    group = "com.github.mickeyxiami.alchemy"
}

allprojects {
    apply plugin: 'java'
    repositories {
        mavenCentral()
        jcenter()
    }

    configurations {
        ecj
    }

    dependencies {
        ecj 'org.eclipse.jdt:ecj:+'
    }

    def jdt = configurations.ecj.asPath

    sourceCompatibility = '10'
    targetCompatibility = '10'
}

File[] fileList(String dir) {
    file(dir).listFiles({ file -> file.isFile() } as FileFilter).sort()
}

File[] dirList(String dir) {
    file(dir).listFiles({ file -> file.isDirectory() } as FileFilter).sort()
}

version = "${config.minecraft_version}-${config.mod_version}.${config.dev_version}"
config.build_number = config.dev_version

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
    distributionType = Wrapper.DistributionType.ALL
}